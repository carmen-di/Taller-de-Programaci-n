programa ej1
procesos
  proceso identificar
  comenzar
    EnviarMensaje(1, r1)
    EnviarMensaje(2, r2)
    EnviarMensaje(3, r3)
  fin
  proceso juntarFlores
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
  fin
areas
  areaS: AreaP(100,100,100,100)
  areaC1: AreaPC(1,1,1,100)
  areaC2: AreaPC(2,1,2,100)
  areaC3: AreaPC(3,1,3,100)
robots
  robot cliente
  variables
    id, cantF, av, ca: numero
    seguir: boolean
  comenzar
    RecibirMensaje(id, rs)
    mientras (PosCa < 100)
      av:= PosAv
      ca:= PosCa +1
      Random(cantF, 1, 4)
      EnviarMensaje(id, rs)
      EnviarMensaje(cantF, rs)
      EnviarMensaje(av, rs)
      EnviarMensaje(ca, rs)
      RecibirMensaje(seguir, rs)
      Pos(av, ca)
      juntarFlores
      Pos(av, ca-1)
      mientras((HayFlorEnLaBolsa) & (PosCa < 100))
        depositarFlor
        mover
        cantF:= cantF - 1 
    EnviarMensaje(0, rs)
  fin
  
  robot servidor
  variables
    flor, av, ca, id, cantTermino:numero
    seguir:boolean
  comenzar
    cantTermino:= 0
    seguir:= V
    identificar
    mientras (cantTermino < 3)
      RecibirMensaje(id,*)
      si (id <> 0)
        si (id = 1)
          RecibirMensaje(flor, r1){recibo la cantidad y empiezo a servir}
          RecibirMensaje(av, r1)
          RecibirMensaje(ca, r1)
        sino
          si(id = 2)
            RecibirMensaje(flor, r2)
            RecibirMensaje(av, r2)
            RecibirMensaje(ca, r2)
          sino
            RecibirMensaje(flor, r3)
            RecibirMensaje(av, r3)
            RecibirMensaje(ca, r3)
        Pos(av,ca)
        repetir flor
          depositarFlor
        Pos(100,100)
        si (id = 1)
          EnviarMensaje(seguir, r1)
        sino
          si(id = 2)
            EnviarMensaje(seguir, r2)
          sino
            EnviarMensaje(seguir, r3)
      sino
        cantTermino:= cantTermino + 1
  fin
  
variables
  r1, r2, r3: cliente
  rs: servidor
comenzar
  AsignarArea(r1, areaC1)
  AsignarArea(r2, areaC2)
  AsignarArea(r3, areaC3)
  AsignarArea(rs, areaS)
  AsignarArea(rs, areaC1)
  AsignarArea(rs, areaC2)
  AsignarArea(rs, areaC3)
  Iniciar(r1, 1, 1)
  Iniciar(r2, 2, 1)
  Iniciar(r3, 3, 1)
  Iniciar(rs, 100, 100)
fin

