programa ej4
procesos
  proceso identificar
  comenzar
    EnviarMensaje(1, r1)
    EnviarMensaje(2, r2)
    EnviarMensaje(3, r3)
  fin
  proceso juntarFlores(ES flores: numero)
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      flores:= flores + 1
  fin
areas
  cuadrante: AreaPC(40,40,60,60)
  areaD: AreaC(10,10,10,10) {area deposito}
  areaF: AreaP(4,1,4,1)
  areaR1: AreaP(1,1,1,1)
  areaR2: AreaP(2,1,2,1)
  areaR3: AreaP(3,1,3,1)
robots
  robot florero
  variables
    cantF, av, ca, x, y, id: numero
    ok: boolean
  comenzar
    av:= PosAv
    ca:= PosCa
    cantF:= 0
    RecibirMensaje(id, rf)
    RecibirMensaje(ok ,rf)
    mientras(ok)
      RecibirMensaje(x, rf)
      RecibirMensaje(y, rf)
      BloquearEsquina(x, y)
      Pos(x, y)
      juntarFlores(cantF)
      Pos(av, ca)
      LiberarEsquina(x, y)
      RecibirMensaje(ok, rf)
    BloquearEsquina(10, 10)
    Pos(10, 10)
    repetir cantF
      depositarFlor
    Pos(av, ca)
    LiberarEsquina(10, 10)
    EnviarMensaje(cantF, rf)
  fin
  
  robot fiscalizador
  variables
    av, ca, total, flores, id: numero
    ok: boolean
  comenzar
    total:= 0
    ok:= V
    identificar
    repetir 8
      Random(id, 1, 3)
      Random(av, 40, 60)
      Random(ca, 40, 60)
      si (id = 1)
        EnviarMensaje(ok, r1)
        EnviarMensaje(av, r1)
        EnviarMensaje(ca, r1)
      sino
        si (id = 2)
          EnviarMensaje(ok, r2)
          EnviarMensaje(av, r2)
          EnviarMensaje(ca, r2)
        sino
          EnviarMensaje(ok, r3)
          EnviarMensaje(av, r3)
          EnviarMensaje(ca, r3)
    EnviarMensaje(F, r1)
    EnviarMensaje(F, r2)
    EnviarMensaje(F, r3)
    repetir 3
      RecibirMensaje(flores, *)
      total:= total + flores
    Informar('Cantidad total', total)
  fin
  
variables
  r1, r2, r3: florero
  rf: fiscalizador
comenzar
  AsignarArea(r1, areaR1)
  AsignarArea(r2, areaR2)
  AsignarArea(r3, areaR3)
  AsignarArea(rf, areaF)
  AsignarArea(r1, cuadrante)
  AsignarArea(r2, cuadrante)
  AsignarArea(r3, cuadrante)
  AsignarArea(r1, areaD)
  AsignarArea(r2, areaD)
  AsignarArea(r3, areaD)
  AsignarArea(rf, areaF)
  Iniciar(r1, 1, 1)
  Iniciar(r2, 2, 1)
  Iniciar(r3, 3, 1)
  Iniciar(rf, 4, 1)
fin